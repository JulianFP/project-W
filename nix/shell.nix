{
  pkgs ? import <nixpkgs> { },
  system,
  inputs,
}:
let
  dontCheckPythonPkg =
    drv:
    drv.overridePythonAttrs (old: {
      doCheck = false;
    });
  myPythonPackages =
    ps: with ps; [
      #all required dependencies + this projects package itself (required for sphinx)
      click
      argon2-cffi
      fastapi
      pydantic
      email-validator
      psycopg
      psycopg-pool
      redis
      authlib
      bonsai
      itsdangerous
      pycryptodome
      aiosmtplib
      granian
      uvicorn
      setproctitle
      python-multipart
      httpx
      platformdirs
      pyaml-env
      watchfiles

      #for the tests: tests
      pytest
      pytest-timeout
      smtpdfix

      #optional dependencies: docs
      sphinx
      sphinxcontrib-openapi
      autodoc-pydantic
      gitpython
      sphinx-mdinclude
      sphinx-rtd-theme
    ];
  pre-commit-check = inputs.pre-commit-hooks.lib.${system}.run {
    src = ./.;
    hooks = {
      end-of-file-fixer.enable = true;
      trim-trailing-whitespace.enable = true;
      check-added-large-files.enable = true;
      check-merge-conflicts.enable = true;
      check-symlinks.enable = true;
      check-docstring-first.enable = true;
      check-builtin-literals.enable = true;
      check-python.enable = true;
      python-debug-statements.enable = true;
      ruff = {
        enable = true;
        excludes = [ "doc/conf.py" ];
      };
      ruff-format.enable = true;
      nixfmt-rfc-style.enable = true;
      cspell = {
        enable = true;
        args = [ "-w" ];
      };
    };
  };
in
pkgs.mkShell {
  buildInputs =
    with pkgs;
    [
      (python313.withPackages myPythonPackages)
      (poetry.override { python3 = python313; })
    ]
    ++ pre-commit-check.enabledPackages;

  shellHook = ''
    localOverwriteFile=".pre-commit-config.yaml"
    if ! grep -q "This file was generated by git-hooks.nix" "$localOverwriteFile"; then
        git update-index --skip-worktree "$localOverwriteFile"
        rm "$localOverwriteFile"
    fi
    git update-index --skip-worktree "config.yml"
  ''
  + pre-commit-check.shellHook;
}
