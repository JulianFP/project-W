{
  forAllSystems,
  inputs,
  #ddesiredDevEnvs is an attribute set of attribute sets containing the values workspaceRoot, pythonPkg, extraPackages
  desiredDevEnvs,
}:
let
  inherit (inputs.nixpkgs) lib;
  workspaces = builtins.mapAttrs (
    name: envAttrs:
    inputs.uv2nix.lib.workspace.loadWorkspace { workspaceRoot = ../. + envAttrs.workspaceRoot; }
  ) desiredDevEnvs;
  overlays = builtins.mapAttrs (
    name: ws: ws.mkPyprojectOverlay { sourcePreference = "wheel"; }
  ) workspaces;
  editableOverlays = builtins.mapAttrs (
    name: ws:
    ws.mkEditablePyprojectOverlay { root = "$REPO_ROOT${desiredDevEnvs.${name}.workspaceRoot}"; }
  ) workspaces;
  pythonSetsSets = builtins.mapAttrs (
    name: overlay:
    forAllSystems (
      system:
      let
        pkgs = import inputs.nixpkgs {
          inherit system;
          config.allowUnfree = true;
        };
        python = (desiredDevEnvs.${name}.pythonPkg pkgs);
      in
      (pkgs.callPackage inputs.pyproject-nix.build.packages {
        inherit python;
      }).overrideScope
        (
          lib.composeManyExtensions [
            inputs.pyproject-build-systems.overlays.wheel
            overlay
            (import ./build_system_overlays.nix {
              inherit (inputs) self;
              inherit pkgs;
            })
          ]
        )
    )
  ) overlays;
in
{
  inherit workspaces pythonSetsSets;

  devShells = forAllSystems (
    system:
    let
      pkgs = import inputs.nixpkgs {
        inherit system;
        config.allowUnfree = true;
      };
      pythonSets = builtins.mapAttrs (
        name: sets: sets.${system}.overrideScope editableOverlays.${name}
      ) pythonSetsSets;
      virtualenvs = builtins.mapAttrs (
        name: set: set.mkVirtualEnv name workspaces.${name}.deps.all
      ) pythonSets;
      pre-commit-check = inputs.pre-commit-hooks.lib.${system}.run {
        src = ./.;
        hooks = {
          end-of-file-fixer.enable = true;
          trim-trailing-whitespace.enable = true;
          check-added-large-files.enable = true;
          check-merge-conflicts.enable = true;
          check-symlinks.enable = true;
          check-docstring-first.enable = true;
          check-builtin-literals.enable = true;
          check-python.enable = true;
          python-debug-statements.enable = true;
          biome = {
            enable = true;
            files = "^frontend/";
            types_or = [ ];
          };
          ruff = {
            enable = true;
            files = "^backend/|^runner/";
          };
          ruff-format = {
            enable = true;
            files = "^backend/|^runner/";
          };
          codespell = {
            enable = true;
            name = "codespell";
            entry = "${pkgs.codespell}/bin/codespell -w --ignore-words-list=delet --skip=frontend/src/lib/utils/schema.d.ts,frontend/pnpm-lock.yaml,frontend/package.json";
          };
          nixfmt-rfc-style.enable = true;
        };
      };
      commonPackages = [
        pkgs.uv
        pkgs.nodejs_24
        pkgs.corepack_24
      ]
      ++ pre-commit-check.enabledPackages;
      commonShellHook = ''
        export REPO_ROOT=$(git rev-parse --show-toplevel)
        localOverwriteFile="$REPO_ROOT/.pre-commit-config.yaml"
        if ! grep -q "This file was generated by git-hooks.nix" "$localOverwriteFile"; then
            git update-index --skip-worktree "$localOverwriteFile"
            rm "$localOverwriteFile"
        fi
        git update-index --skip-worktree "$REPO_ROOT/backend/config.yml"
        git update-index --skip-worktree "$REPO_ROOT/runner/config.yml"
      ''
      + pre-commit-check.shellHook;
    in
    (builtins.mapAttrs (
      name: venv:
      pkgs.mkShell {
        buildInputs = pre-commit-check.enabledPackages;
        packages = [
          venv
          inputs.pyproject-nix.packages.${system}.build-editable
        ]
        ++ commonPackages
        ++ (desiredDevEnvs.${name}.extraPackages pkgs);
        env = {
          UV_NO_SYNC = "1";
          UV_PYTHON = pythonSets.${name}.python.interpreter;
          UV_PYTHON_DOWNLOADS = "never";
        };
        shellHook = commonShellHook + ''
          unset PYTHONPATH
          . ${venv}/bin/activate
        '';
      }
    ) virtualenvs)
    // {
      root = pkgs.mkShell {
        buildInputs = commonPackages;
        shellHook = commonShellHook;
      };
    }
  );
}
