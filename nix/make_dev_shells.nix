{
  forAllSystems,
  inputs,
  #ddesiredDevEnvs is an attribute set of attribute sets containing the values workspaceRoot, pythonPkg, extraPackages
  desiredDevEnvs,
}:
let
  inherit (inputs.nixpkgs) lib;
  workspaces = builtins.mapAttrs (
    name: envAttrs:
    inputs.uv2nix.lib.workspace.loadWorkspace { workspaceRoot = ../. + envAttrs.workspaceRoot; }
  ) desiredDevEnvs;
  overlays = builtins.mapAttrs (
    name: ws: ws.mkPyprojectOverlay { sourcePreference = "wheel"; }
  ) workspaces;
  editableOverlays = builtins.mapAttrs (
    name: ws:
    ws.mkEditablePyprojectOverlay { root = "$REPO_ROOT${desiredDevEnvs.${name}.workspaceRoot}"; }
  ) workspaces;
  pythonSetsSets = builtins.mapAttrs (
    name: overlay:
    forAllSystems (
      system:
      let
        pkgs = import inputs.nixpkgs {
          inherit system;
          config.allowUnfree = true;
        };
        python = (desiredDevEnvs.${name}.pythonPkg pkgs);
      in
      (pkgs.callPackage inputs.pyproject-nix.build.packages {
        inherit python;
      }).overrideScope
        (
          lib.composeManyExtensions [
            inputs.pyproject-build-systems.overlays.wheel
            overlay
            (import ./build_system_overlays.nix {
              inherit (inputs) self;
              inherit pkgs;
            })
          ]
        )
    )
  ) overlays;
in
{
  inherit workspaces pythonSetsSets;

  devShells = forAllSystems (
    system:
    let
      pkgs = import inputs.nixpkgs {
        inherit system;
        config.allowUnfree = true;
      };
      pythonSets = builtins.mapAttrs (
        name: sets: sets.${system}.overrideScope editableOverlays.${name}
      ) pythonSetsSets;
      virtualenvs = builtins.mapAttrs (
        name: set: set.mkVirtualEnv name workspaces.${name}.deps.all
      ) pythonSets;
      commonPackages = [
        pkgs.uv
        pkgs.nodejs_24
        pkgs.corepack_24
      ]
      ++ inputs.self.checks.${system}.pre-commit-check.enabledPackages;
      commonShellHook = ''
        export REPO_ROOT=$(git rev-parse --show-toplevel)
        localOverwriteFile="$REPO_ROOT/.pre-commit-config.yaml"
        if ! grep -q "This file was generated by git-hooks.nix" "$localOverwriteFile"; then
            git update-index --skip-worktree "$localOverwriteFile"
            rm "$localOverwriteFile"
        fi
        git update-index --skip-worktree "$REPO_ROOT/backend/config.yml"
        git update-index --skip-worktree "$REPO_ROOT/runner/config.yml"
      ''
      + inputs.self.checks.${system}.pre-commit-check.shellHook;
    in
    (builtins.mapAttrs (
      name: venv:
      pkgs.mkShell {
        buildInputs = commonPackages;
        packages = [
          venv
          inputs.pyproject-nix.packages.${system}.build-editable
        ]
        ++ (desiredDevEnvs.${name}.extraPackages pkgs);
        env = {
          UV_NO_SYNC = "1";
          UV_PYTHON = pythonSets.${name}.python.interpreter;
          UV_PYTHON_DOWNLOADS = "never";
        };
        shellHook = commonShellHook + ''
          unset PYTHONPATH
          . ${venv}/bin/activate
        '';
      }
    ) virtualenvs)
    // {
      root = pkgs.mkShell {
        buildInputs = commonPackages;
        shellHook = commonShellHook;
      };
    }
  );
}
